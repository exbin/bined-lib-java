plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

apply from: '../apps.gradle'

ext.depsRoot = 'BinedJava'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.bined.swing.example.BinEdExample'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.mainClass = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    flatDir {
        dirs "../../lib"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation moduleDep('BinedJava', 'org.exbin.bined', 'modules', 'bined-core')
    implementation moduleDep('BinedJava', 'org.exbin.bined', 'modules', 'bined-swing')
    implementation moduleDep('BinedJava', 'org.exbin.bined', 'modules', 'bined-section')
    implementation moduleDep('BinedJava', 'org.exbin.bined', 'modules', 'bined-swing-section')
    implementation moduleDep('ExbinAuxiliaryJava', 'org.exbin.auxiliary', 'modules', 'binary_data')
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

