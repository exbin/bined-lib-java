buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }    
    }
    dependencies {
        if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
            classpath("org.openjfx:javafx-plugin:0.0.8")
        }
    }
}

plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    apply plugin: "org.openjfx.javafxplugin"
}

apply from: '../apps.gradle'

ext.depsRoot = 'BinedLibJava'

sourceCompatibility = '1.8'

def javaFxHome = "${System.properties['java.home']}";

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.bined.javafx.example.BinEdExample'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.mainClass = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    flatDir {
        dirs "../../lib"
    }
    mavenLocal()
    mavenCentral()
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    javafx {
        version = "14"
        modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml' ]
    }
}

dependencies {
    //    providedImplementation files("$javaFxHome/lib/jfxrt.jar")
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        implementation group: 'org.openjfx', name: 'javafx-base', version: '11.0.2'
        implementation group: 'org.openjfx', name: 'javafx-controls', version: '11.0.2'
        implementation group: 'org.openjfx', name: 'javafx-graphics', version: '11.0.2'
        implementation group: 'org.openjfx', name: 'javafx-fxml', version: '11.0.2'
    }
    implementation moduleDep('BinedLibJava', 'org.exbin.bined', 'modules', 'bined-core')
    implementation moduleDep('BinedLibJava', 'org.exbin.bined', 'modules', 'bined-javafx')
    implementation moduleDep('ExbinAuxiliaryJava', 'org.exbin.auxiliary', 'modules', 'binary_data')
    implementation moduleDep('ExbinAuxiliaryJava', 'org.exbin.auxiliary', 'modules', 'binary_data-array')
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

