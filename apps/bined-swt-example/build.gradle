plugins {
    id 'java'
    id 'distribution'
    id 'maven-publish'
}

apply from: '../apps.gradle'

ext.depsRoot = 'BinedLibJava'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'org.exbin.bined.swt.example.BinEdExample'
    ext.appArgs = '-dev'
}

def configureExec(task) {
    task.mainClass = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    if (hasProperty('appArgs')) {
        task.args appArgs
    }
}

task run(type: JavaExec) {
    task -> configureExec(task)
}

task debug(type: JavaExec) {
    task -> configureExec(task)
    debug = true
}

repositories {
    flatDir {
        dirs "../../lib"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation moduleDep('BinedLibJava', 'org.exbin.bined', 'modules', 'bined-core')
    implementation moduleDep('BinedLibJava', 'org.exbin.bined', 'modules', 'bined-swt')
    //    implementation moduleDep('BinedLibJava', 'org.exbin.bined', 'modules', 'bined-swt-extended')
    implementation moduleDep('ExbinAuxiliaryJava', 'org.exbin.auxiliary', 'modules', 'binary_data')
    implementation group: 'org.eclipse.swt', name: 'org.eclipse.swt.gtk.linux.x86_64', version: '4.3'
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

def manifestClassPath() {
    try {
        return (configurations.runtimeClasspath.collect { it.getName() }).join(' ')
    } catch (Exception) {
        return ""
    }
}

jar {
    manifest {
        attributes(
            'Main-Class': mainClass,
            'Class-Path': manifestClassPath()
        )
    }
}

