buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }    
    }
    dependencies {
        if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
            classpath("org.openjfx:javafx-plugin:0.0.8")
        }
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    apply plugin: "org.openjfx.javafxplugin"
}

group = 'org.exbin.bined'
ext.libraryName = 'BinEd JavaFX Library'
ext.libraryDescription = 'Library for BinEd editor component'

apply from: '../bined-java-modules.gradle'

//def javaFxHome = "${System.properties['java.home']}"

sourceCompatibility = '1.8'

targetCompatibility = '1.8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    flatDir {
        dirs "../../lib"
    }
    mavenLocal()
    mavenCentral()
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    javafx {
        version = "14"
        modules = [ 'javafx.controls', 'javafx.graphics' ]
    }
}

dependencies {
    api moduleDep('BinedLibJava', 'org.exbin.bined', 'modules', 'bined-core')
    //    providedImplementation files("$javaFxHome/lib/jfxrt.jar")
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        implementation group: 'org.openjfx', name: 'javafx-base', version: '11.0.2'
        implementation group: 'org.openjfx', name: 'javafx-controls', version: '11.0.2'
        implementation group: 'org.openjfx', name: 'javafx-graphics', version: '11.0.2'
    }
    implementation moduleDep('ExbinAuxiliaryJava', 'org.exbin.auxiliary', 'modules', 'binary_data')
    implementation moduleDep('ExbinAuxiliaryJava', 'org.exbin.auxiliary', 'modules', 'binary_data-array')
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testImplementation group : 'junit', name: 'junit', version: '4.13.2'
}
